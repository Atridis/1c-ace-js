
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработка =  РеквизитФормыВЗначение("Объект");
	
	Для Каждого Макет Из Обработка.Метаданные().Макеты Цикл
		Если СтрНачинаетсяС(Макет.Имя, "mode_") Тогда
			Элементы.Синтаксис.СписокВыбора.Добавить(СтрЗаменить(Макет.Имя, "mode_", ""));
		КонецЕсли; 
		Если СтрНачинаетсяС(Макет.Имя, "theme_") Тогда
			Элементы.Тема.СписокВыбора.Добавить(СтрЗаменить(Макет.Имя, "theme_", ""));
		КонецЕсли; 
	КонецЦикла;
	
	Если Элементы.Синтаксис.СписокВыбора.Количество() > 0 Тогда
		Синтаксис = Элементы.Синтаксис.СписокВыбора[0];
	КонецЕсли; 
	
	Если Элементы.Тема.СписокВыбора.Количество() > 0 Тогда
		Тема = Элементы.Тема.СписокВыбора[0];
	КонецЕсли; 
	
	ЗагрузитьТемуСинтаксис();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СинтаксисПриИзменении(Элемент)
	ЗагрузитьТемуСинтаксис();
КонецПроцедуры

&НаКлиенте
Процедура ТемаПриИзменении(Элемент)
	ЗагрузитьТемуСинтаксис();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьТемуСинтаксис()
	
	Обработка =  РеквизитФормыВЗначение("Объект");
	ШаблонСтраницы = Обработка.ПолучитьМакет("html_template").ПолучитьТекст(); 
	Эйс = Обработка.ПолучитьМакет("ace").ПолучитьТекст(); 
	
	ТекстСинтаксиса = Обработка.ПолучитьМакет("mode_" + Синтаксис).ПолучитьТекст();
	ТекстТемы       = Обработка.ПолучитьМакет("theme_" + Тема).ПолучитьТекст();
	
	Если Обработка.Метаданные().Макеты.Найти("example_" + Синтаксис) <> Неопределено Тогда
		ТекстПримера = Обработка.ПолучитьМакет("example_" + Синтаксис).ПолучитьТекст();
	Иначе
		ТекстПримера = "Hello world!";
	КонецЕсли; 
	
	ГотовыйТекст = ШаблонСтраницы;
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{ace}}",        Эйс);
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{theme}}",      ТекстТемы);
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{theme_name}}", Тема);
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{mode}}",       ТекстСинтаксиса);
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{mode_name}}",  Синтаксис);
	ГотовыйТекст = СтрЗаменить(ГотовыйТекст, "{{editor}}",     ТекстПримера);
	
	Редактор = ГотовыйТекст;
	
КонецПроцедуры

#КонецОбласти
